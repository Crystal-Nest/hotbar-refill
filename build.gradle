buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "commons-io:commons-io:2.17.0"
  }
}

plugins {
  id "java"
  id "java-library"
  id "maven-publish"
  id "com.hypherionmc.modutils.modpublisher" version "2.2.0+snapshot.1"
  id "org.jetbrains.gradle.plugin.idea-ext" version "1.3"
  id "org.sonarqube" version "7.2.2.6593"
}

import org.gradle.internal.os.OperatingSystem

ext {
  if (project.hasProperty('hytale_home')) {
    hytaleHome = project.findProperty('hytale_home')
  } else {
    def os = OperatingSystem.current()
    if (os.isWindows()) {
      hytaleHome = "${System.getProperty("user.home")}/AppData/Roaming/Hytale"
    } else if (os.isMacOsX()) {
      hytaleHome = "${System.getProperty("user.home")}/Library/Application Support/Hytale"
    } else if (os.isLinux()) {
      hytaleHome = "${System.getProperty("user.home")}/.var/app/com.hypixel.HytaleLauncher/data/Hytale"
      if (!file(hytaleHome).exists()) {
        hytaleHome = "${System.getProperty("user.home")}/.local/share/Hytale"
      }
    }
  }
}

if (!project.hasProperty('hytaleHome')) {
  logger.warn("Your Hytale install could not be detected automatically. If you are on an unsupported platform or using a custom install location, please define the install location using the hytale_home property.")
}
else if (!file(project.findProperty('hytaleHome')).exists()) {
  logger.warn("Failed to find Hytale at the expected location. The expected location can be changed using the hytale_home property. Currently looking in ${project.findProperty('hytaleHome')}")
}

java {
  toolchain.languageVersion = JavaLanguageVersion.of(java_version)
  withSourcesJar()
  withJavadocJar()
}

// Quiet warnings about missing Javadocs.
javadoc {
  options.addStringOption('Xdoclint:-missing', '-quiet')
}

repositories {
  mavenCentral()
  exclusiveContent {
    forRepositories(
      maven {
        name = "hytale-pre-release"
        url = uri("https://maven.hytale.com/pre-release")
      },
      maven {
        name = "hytale-release"
        url = uri("https://maven.hytale.com/release")
      }
    )
    filter {
      includeGroup "com.hypixel.hytale"
    }
  }
}

// Add the Hytale server as a build dependency, allowing you to reference and compile against their code.
dependencies {
  implementation "com.hypixel.hytale:Server:$hytale_version"
}

// Create the working directory to run the server if it does not already exist.
def serverRunDir = file("$projectDir/run")
if (!serverRunDir.exists()) {
  serverRunDir.mkdirs()
}

// Makes sure the plugin manifest is up to date.
tasks.named('processResources') {
  doLast {
    // Locate the manifest file in the build output directory
    def builtManifest = file("${sourceSets.main.output.resourcesDir}/manifest.json")
    if (builtManifest.exists()) {
      def manifestJson = new groovy.json.JsonSlurper().parseText(builtManifest.text)
      manifestJson.Group = plugin_group
      manifestJson.Name = plugin_title
      manifestJson.Version = mod_version
      manifestJson.Description = mod_description
      manifestJson.Main = "$maven_group.${plugin_id.replace('-', '_')}.$plugin_name"
      manifestJson.Website = "https://github.com/$github_user/$plugin_id"
      manifestJson.IncludesAssetPack = includes_pack.toBoolean()
      manifestJson.Authors = authors.split(", ").collect { [Name: it] }
      builtManifest.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(manifestJson))
    }
  }
}

def createServerRunArguments(String srcDir) {
  def programParameters = '--allow-op --disable-sentry --assets="' + "${hytaleHome}/install/release/package/game/latest/Assets.zip" + '"'
  def modPaths = []
  if (includes_pack.toBoolean()) {
    modPaths << srcDir
  }
  if (!modPaths.isEmpty()) {
    programParameters += ' --mods="' + modPaths.join(',') + '"'
  }
  return programParameters
}

// Creates a run configuration in IDEA that will run the Hytale server with your plugin and the default assets.
idea.project.settings.runConfigurations {
  'HytaleServer'(org.jetbrains.gradle.ext.Application) {
    mainClass = 'com.hypixel.hytale.Main'
    moduleName = project.idea.module.name + '.main'
    programParameters = createServerRunArguments(sourceSets.main.java.srcDirs.first().parentFile.absolutePath)
    workingDirectory = serverRunDir.absolutePath
  }
}

sonar {
  properties {
    property "sonar.projectKey", "Crystal-Nest_${plugin_id.replace('-', '_')}"
    property "sonar.organization", "crystal-nest"
    property "sonar.projectVersion", mod_version
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.qualitygate.wait", "true"
  }
}

publishing {
  repositories {
    maven {
      name = "GitHubPackages"
      url = uri("https://maven.pkg.github.com/$github_user/$plugin_id")
      credentials {
        username = github_user
        password = System.getenv("GITHUB_TOKEN") ?: ""
      }
    }
  }
  publications {
    gpr(MavenPublication) {
      groupId = maven_group
      artifactId = plugin_id
      version = project.mod_version
      from components.java
      pom {
        name = "$plugin_title v$mod_version"
        description = "$mod_description"
        url = "https://github.com/$github_user/$plugin_id"
        issueManagement {
          system = "github"
          url = "https://github.com/$github_user/$plugin_id/issues"
        }
        licenses {
          license {
            name = license
            url = "https://github.com/Crystal-Nest/.github/blob/main/LICENSE"
          }
        }
        developers {
          authors.split(", ").each { author ->
            developer {
              id = author.toLowerCase()
              name = author
            }
          }
        }
      }
    }
  }
}

def changelog_body = "See [Changelog](https://github.com/$github_user/$plugin_id/blob/master/CHANGELOG.md#v${mod_version.replaceAll('\\.', '')}---${new Date().format("yyyyMMdd")})."
def version_type = mod_version.replaceFirst("([0-9]+\\.?)+-?", "").with { it ? it : (mod_version.startsWith("0") ? "beta" : "release") }

publisher {
  apiKeys {
    github System.getenv("GITHUB_TOKEN") ?: ""
    modtale System.getenv("MODTALE_TOKEN") ?: ""
    curseforge System.getenv("CURSEFORGE_TOKEN") ?: ""
  }
  setGameType("hytale")
  setVersion(project.mod_version)
  setChangelog(changelog_body)
  setVersionType(version_type)
  setArtifact(jar)
  setDisableMalwareScanner(true) // Disable the built in Fractureizer scanner.
  setDisableEmptyJarCheck(true) // Disable check for valid mod metadata entry in artifact, which could possibly mean that the jar is empty.
  setModtaleID(plugin_id)
  setCurseID(curseforge_id)
  setGameVersions(["early access", "2026.01.22-6f8bdbdc4", "2026.01.23-d5ecebca9", "2026.01.23-6e2d4fc36"])
  setDisplayName("$plugin_title v$project.mod_version")
  setJavaVersions([JavaVersion.VERSION_25]) // Remember to update this to match java_version gradle property.
  github {
    repo = "$github_user/$plugin_id"
    tag = "v${project.mod_version}"
    displayName("v${project.mod_version}")
    target = "main"
    createTag = true
    createRelease = true
    updateRelease = true
    draft = false
  }
}

tasks.register("publishPlugin") {
  group = "publishing"
  description = "Publish the plugin to all platforms"
  dependsOn "publish"
  dependsOn "publishMod"
}
