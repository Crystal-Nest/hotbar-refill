plugins {
  id 'java'
  id 'org.jetbrains.gradle.plugin.idea-ext' version '1.3'
}

import org.gradle.internal.os.OperatingSystem

ext {
  if (project.hasProperty('hytale_home')) {
    hytaleHome = project.findProperty('hytale_home')
  }
  else {
    def os = OperatingSystem.current()
    if (os.isWindows()) {
      hytaleHome = "${System.getProperty("user.home")}/AppData/Roaming/Hytale"
    }
    else if (os.isMacOsX()) {
      hytaleHome = "${System.getProperty("user.home")}/Library/Application Support/Hytale"
    }
    else if (os.isLinux()) {
      hytaleHome = "${System.getProperty("user.home")}/.var/app/com.hypixel.HytaleLauncher/data/Hytale"
      if (!file(hytaleHome).exists()) {
        hytaleHome = "${System.getProperty("user.home")}/.local/share/Hytale"
      }
    }
  }
}

if (!project.hasProperty('hytaleHome')) {
  throw new GradleException('Your Hytale install could not be detected automatically. If you are on an unsupported platform or using a custom install location, please define the install location using the hytale_home property.');
}
else if (!file(project.findProperty('hytaleHome')).exists()) {
  throw new GradleException("Failed to find Hytale at the expected location. Please make sure you have installed the game. The expected location can be changed using the hytale_home property. Currently looking in ${project.findProperty('hytaleHome')}")
}

java {
  toolchain.languageVersion = JavaLanguageVersion.of(25)
  withSourcesJar()
  withJavadocJar()
}

// Quiet warnings about missing Javadocs.
javadoc {
  options.addStringOption('Xdoclint:-missing', '-quiet')
}

// Adds the Hytale server as a build dependency, allowing you to reference and compile against their code. This requires you to have Hytale installed using the official launcher for now.
dependencies {
  implementation(files("$hytaleHome/install/release/package/game/latest/Server/HytaleServer.jar"))
}

// Create the working directory to run the server if it does not already exist.
def serverRunDir = file("$projectDir/run")
if (!serverRunDir.exists()) {
  serverRunDir.mkdirs()
}

// Makes sure the plugin manifest is up to date.
tasks.named('processResources') {
  doLast {
    // Locate the manifest file in the build output directory
    def builtManifest = file("${sourceSets.main.output.resourcesDir}/manifest.json")
    if (builtManifest.exists()) {
      def manifestJson = new groovy.json.JsonSlurper().parseText(builtManifest.text)
      manifestJson.Group = plugin_group
      manifestJson.Name = plugin_name
      manifestJson.Version = version
      manifestJson.Description = description
      manifestJson.Main = "$maven_group.${plugin_id.replace('-', '_')}.$plugin_name"
      manifestJson.Website = "https://github.com/Crystal-Nest/$plugin_id"
      manifestJson.IncludesAssetPack = includes_pack.toBoolean()
      builtManifest.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(manifestJson))
    }
  }
}

def createServerRunArguments(String srcDir) {
  def programParameters = '--allow-op --disable-sentry --assets="' + "${hytaleHome}/install/release/package/game/latest/Assets.zip" + '"'
  def modPaths = []
  if (includes_pack.toBoolean()) {
    modPaths << srcDir
  }
  if (!modPaths.isEmpty()) {
    programParameters += ' --mods="' + modPaths.join(',') + '"'
  }
  return programParameters
}

// Creates a run configuration in IDEA that will run the Hytale server with your plugin and the default assets.
idea.project.settings.runConfigurations {
  'HytaleServer'(org.jetbrains.gradle.ext.Application) {
    mainClass = 'com.hypixel.hytale.Main'
    moduleName = project.idea.module.name + '.main'
    programParameters = createServerRunArguments(sourceSets.main.java.srcDirs.first().parentFile.absolutePath)
    workingDirectory = serverRunDir.absolutePath
  }
}
